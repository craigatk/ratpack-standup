buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:1.4.2"
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
  }
}

plugins {
  id "com.moowork.node" version "0.13"
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "idea"

repositories {
  jcenter()
}

dependencies {
  compile ratpack.dependency("guice")
  compile 'com.h2database:h2:1.4.190'
  compile 'org.jdbi:jdbi:2.77'
  compile 'org.flywaydb:flyway-core:4.0.3'

  runtime "org.slf4j:slf4j-simple:1.7.21"

  testCompile ratpack.dependency('test')
  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testCompile 'cglib:cglib:2.2.2'
  testCompile 'org.objenesis:objenesis:2.1'

  testCompile("org.gebish:geb-spock:1.0") {
    exclude group: "org.codehaus.groovy", module: "groovy-all"
  }
  testCompile "org.seleniumhq.selenium:selenium-chrome-driver:2.53.1"
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:2.53.1"

  // For downloading browser-specific drivers that browsers like Chrome and IE require
  // From https://github.com/bonigarcia/webdrivermanager
  testCompile("io.github.bonigarcia:webdrivermanager:1.4.9") {
    exclude group: 'org.seleniumhq.selenium'
  }

  // For testing WebSockets
  testCompile 'org.java-websocket:Java-WebSocket:1.3.0'
}

node {
  // Version of node to use.
  version = '6.8.1'

  // If true, it will download node using above parameters.
  // If false, it will try to use globally installed node.
  download = true

  // Set the work directory for unpacking node
  workDir = file("${project.buildDir}/nodejs")

  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${project.projectDir}/src/app")
}

task npmShrinkwrap(type: NpmTask, dependsOn: 'nodeSetup') {
  args = ['shrinkwrap', '--dev']
}

test {
  exclude '**/*GebSpec*'
}

task testBrowser(type: Test, dependsOn: 'assembleJs') {
  include '**/*GebSpec*'

  // Pass system properties through to the testBrowser task so we can pass in the 'geb.env' property to run tests
  // in different browsers. Adapted from http://mrhaki.blogspot.com/2015/09/grails-goodness-passing-system.html
  systemProperties System.properties
}
check.dependsOn('testBrowser')

task testJs(type: NpmTask, dependsOn: 'npmInstall') {
  inputs.dir('src/app/test/unit')
  outputs.dir('src/app/test/reports')

  args = ['run', 'test']
}
check.dependsOn('testJs')

task assembleJs(type: NpmTask, dependsOn: 'npmInstall') {
  inputs.dir('src/app')
  outputs.dir('src/ratpack/static/assets')
  outputs.file('src/ratpack/static/index.html')
  args = ['run', 'build']
}
shadowJar.dependsOn('assembleJs')